task copyAndRenamePackage(type: Copy) {
    if(!project.useApachePackageName) {
        doFirst {
            delete new File('src/legacyRelease/java')
        }
        from new File('src/main/java/com/taobao/weex')
        into new File('src/legacyRelease/java/org/apache/weex')
        filter { String line ->
            line.replaceAll(/^(package com\.taobao)(\.weex.*)$/, { _, packageName, suffix ->
                "package org.apache${suffix}"
            }).replaceAll(/^(import com\.taobao)(\.weex.*)$/, { _, packageName, suffix ->
                "import org.apache${suffix}"
            }).replaceAll(/^(import static com\.taobao)(\.weex.*)$/, { _, packageName, suffix ->
                "import static org.apache${suffix}"
            })
        }
    }
}

task copyManifest(type: Copy){
    if(!project.useApachePackageName){
        doFirst {
            delete new File('src/legacyRelease/AndroidManifest.xml')
        }
        from new File('src/main/AndroidManifest.xml')
        into new File('src/legacyRelease')
        filter { String line ->
            line.replaceAll(/(com\.taobao)(\.weex.*)/, { _, packageName, suffix ->
                "org.apache${suffix}"
            })
        }
    }
}

task copyOtherCppFile(type: Copy){
    if(!project.useApachePackageName) {
        doFirst {
            delete new File('src/legacyRelease/cpp')
        }
        from new File('../../weex_core')
        into new File('src/legacyRelease/cpp')
        exclude '**/*.cpp', '**/*.cc', '**/*.c', '**/*.h', '**/*.hpp'
    }
}

task copyAndRenameCppSourceFile(type: Copy, dependsOn: copyOtherCppFile){
    if(!project.useApachePackageName) {
        from new File('../../weex_core')
        into new File('src/legacyRelease/cpp')
        include '**/*.cpp', '**/*.cc', '**/*.c', '**/*.h', '**/*.hpp'
        filter { String line ->
            line.replaceAll('(.*".*)(com/taobao/weex)(.*".*)', { _, prefix, packageName, suffix ->
                logger.info("Content substation in .cpp/.h files happpened, \n Input: ${line}, \n Output: ${prefix}org/apache/weex${suffix}")
                "${prefix}org/apache/weex${suffix}"
            })
        }
    }
}